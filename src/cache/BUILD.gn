shared_library("file_cache") {
  visibility = [
    "//src/daemon:*",
    "//src/test:unit_tests",
  ]

  sources = [
    "database_leveldb.cc",
    "database_leveldb.h",
    "file_cache.cc",
    "file_cache.h",
  ]

  public = [
    "file_cache.h",
  ]

  configs += [ "//build/config:libclang_includes" ]

  # FIXME: make the libclang a shared library.
  if (!config_for_tests || host_os == "mac") {
    configs += [ "//build/config:libclang" ]
  }

  deps += [
    ":manifest",
    "//src/base:base",
    "//src/base:logging",
    "//src/perf:stat_service",
    "//src/third_party/leveldb:leveldb",
    "//src/third_party/protobuf:protobuf",
    "//src/third_party/snappy:snappy",
  ]
}

shared_library("manifest") {
  visibility = [
    ":*",
    "//src/test:unit_tests",
  ]

  sources = [
    "manifest.pb.cc",
    "manifest.pb.h",
    "manifest.proto",
    "manifest_utils.cc",
    "manifest_utils.h",
  ]

  if (!config_for_tests) {
    configs -= [ "//build/config:no_rtti" ]
  }

  deps += [ "//src/base:base" ]

  public_deps = [
    "//src/third_party/protobuf:protobuf",
  ]
}
